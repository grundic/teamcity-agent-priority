buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'com.github.rodm', name: 'gradle-teamcity-plugin', version: '0.9.1'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.rodm.teamcity-server'

group = 'com.github.grundic'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        url "http://repository.jetbrains.com/all"
    }
    maven {
        url "http://download.jetbrains.com/teamcity-repository"
    }
    mavenLocal()
}

ext {
    teamcityVersion = System.properties['teamcity.version'] ?: '10.0.2'
    teamcityDir = "${System.properties['user.home']}/.teamcity.d"
    java8Home = project.hasProperty('java8.home') ? property('java8.home') : '/Library/Java/JavaVirtualMachines/jdk1.8.0_65.jdk/Contents/Home'
    serverOpts = "-DTC.res.disableAll=true " +
            "-Dteamcity.development.mode=true " +
            "-Dteamcity.development.shadowCopyClasses=true " +
            "-Xdebug -Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=50055"
}

test {
    useTestNG()
}

dependencies {
    compile "org.jetbrains.teamcity:server-api:${teamcityVersion}"
    /* This dependency is not provided by JetBrains,
     * so you have to install it manually from TeamCity distribution
     * in order to be able to build plugin:
     *
     * mvn install:install-file -Dfile=webapps/ROOT/WEB-INF/lib/web.jar\
     * -DgroupId=org.jetbrains.teamcity -DartifactId=web -Dversion=10.0.2 -Dpackaging=jar
     */
    provided "org.jetbrains.teamcity:web:${teamcityVersion}"
    provided group: 'com.google.code.gson', name: 'gson', version: '2.4'
    testCompile "org.mockito:mockito-core:1.9.5"
}

teamcity {
    version = teamcityVersion

    server {
        downloadsDir = "${teamcityDir}/_downloads"

        descriptor = file("teamcity-plugin.xml")
        tokens = [Version: project.version]

        environments {
            teamcity10 {
                version = '10.0.2'
                downloadUrl = "https://download.jetbrains.com/teamcity/TeamCity-10.0.2.tar.gz"
                homeDir = file("$teamcityDir/TeamCity-${version}")
                dataDir = file("$teamcityDir/data/${version}")
                javaHome = file(java8Home)
                serverOptions = serverOpts
            }
        }
    }
}
